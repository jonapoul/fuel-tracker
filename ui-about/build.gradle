apply plugin: "com.jaredsburrows.license"

android {
    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
}

licenseReport {
    /* We only want JSON data to be put in the app assets, to read at runtime */
    generateJsonReport = true
    copyJsonReportToAssets = true

    /* Don't generate CSV/HTML output */
    generateCsvReport = false
    generateHtmlReport = false
    copyCsvReportToAssets = false
    copyHtmlReportToAssets = false
}

afterEvaluate {
    /* Make sure the licenses JSON is generated whenever we build the app */
    preBuild.dependsOn licenseReleaseReport
    assembleDebug.dependsOn licenseReleaseReport
    assembleRelease.dependsOn licenseReleaseReport

    /* Delete the existing JSON file before we regenerate it. If we don't do this, the plugin
     * doesn't overwrite the existing one so any dependency changes won't be reflected */
    tasks.licenseReleaseReport.doFirst {
        def file = new File(project.projectDir, "src/main/assets/open_source_licenses.json")
        file.delete()
    }
}

dependencies {
    api("com.github.jonapoul:about:$versions.about") {
        exclude group: "org.threeten"
    }
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"
}
